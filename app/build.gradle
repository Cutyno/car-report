apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "me.kuehle.carreport"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 53
        versionName "3.12.0"
    }
    signingConfigs {
        release {
            keyAlias carReportSignKeyAlias
            keyPassword carReportSignKeyPassword
            storeFile file(carReportSignStoreFile)
            storePassword carReportSignStorePassword
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Currently this changes the size of resources from 1932KB to 1943KB, making it useless.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        foss
        full
    }
    lintOptions {
        checkReleaseBuilds false
    }
    packagingOptions {
        // These files are included in at least two of the following libraries:
        // joda-time-2.6.jar, httpmime-4.0.3.jar, httpcore-4.0.1.jar, commons-logging-1.1.1.jar
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
}

repositories {
    // Required for HelloCharts for Android
    maven { url "https://jitpack.io" }
}

dependencies {
    // Charts
    compile 'com.github.gudatcomputers:hellocharts-android:ba603174d302931c0985afb4e54d597fba28146b'

    // Dropbox
    compile 'com.dropbox.core:dropbox-core-sdk:3.0.3'

    // Support, AppCompat
    compile 'com.android.support:support-v4:25.4.0'
    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support:cardview-v7:25.4.0'
    compile 'com.android.support:recyclerview-v7:25.4.0'
    compile 'com.android.support:design:25.4.0'

    // Play Services: for Google Drive
    fullCompile 'com.google.android.gms:play-services-auth:11.0.2'
    fullCompile 'com.google.android.gms:play-services-drive:11.0.2'

    // The new Android Drive API, which is included in the play services, has caching bugs and does
    // not provide all functionality. Until this is fixed we have to use the old Google API.
    fullCompile('com.google.oauth-client:google-oauth-client:1.21.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullCompile files('libs/google-api-client-1.21.0-self-build.jar')
    fullCompile('com.google.apis:google-api-services-drive:v3-rev18-1.21.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'google-api-client'
    }
    fullCompile('com.google.api-client:google-api-client-android:1.21.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'google-api-client'
    }
    fullCompile('com.google.http-client:google-http-client-android:1.21.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude module: 'google-api-client'
    }

    // WebDAV
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    // UI Helpers
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.2'

    // Joda-Time for Android
    compile 'net.danlew:android.joda:2.9.9'
}