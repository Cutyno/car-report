apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "me.kuehle.carreport"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 38
        versionName "3.6.3"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        foss {}
        full {}
    }
    packagingOptions {
        // These files are included in at least two of the following libraries:
        // joda-time-2.6.jar, httpmime-4.0.3.jar, httpcore-4.0.1.jar, commons-logging-1.1.1.jar
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
}

repositories {
    // Required for com.google.api-client
    mavenCentral()
}

dependencies {
    // ChartLib
    compile project(':chartlib')

    // Dropbox
    compile 'com.dropbox.core:dropbox-core-sdk:2.0-beta-1'

    // Support, AppCompat
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'

    // Play Services: for Google Drive
    fullCompile 'com.google.android.gms:play-services-plus:7.5.0'
    fullCompile 'com.google.android.gms:play-services-drive:7.5.0'

    // The new Android Drive API, which is included in the play services, has caching bugs and does
    // not provide all functionality. Until this is fixed we have to use the old Google API.
    fullCompile 'com.google.apis:google-api-services-drive:v2-rev105-1.17.0-rc'
    fullCompile('com.google.api-client:google-api-client-android:1.17.0-rc') {
        exclude group: 'com.google.android.google-play-services'
    }
    fullCompile('com.google.http-client:google-http-client-gson:1.17.0-rc') {
        exclude module: 'httpclient'
    }

    // WebDAV
    compile 'org.apache.jackrabbit:jackrabbit-webdav:2.10.1'

    // UI Helper Libraries
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.melnykov:floatingactionbutton:1.1.0'

    // Misc
    compile 'joda-time:joda-time:2.6'
}
